Include "../engine/assets/prefabs/ferris_wheel/ferris_wheel.bb"

; create window and scene
CreateWindow(1920,1080,"Yo Gabba Gabba Hotel",1)
; CreateWindow(1280,720,"Yo Gabba Gabba Hotel",0)
Clear2D()
Draw2DText "Loading please wait...",5,5
PollEvents()
RenderScene()
Present()

; create camera and light
camera = CreatePerspectiveCamera()

MoveEntity camera,10,2,-10
SetCSMTextureSize 2048
SetCSMSplitDistances 8,32,64,128

light = CreateDirectionalLight()
TurnEntity light,-15,0,0
SetLightColor light,1,1,0.8,0.5
SetLightShadowMappingEnabled light,True

point_light = CreatePointLight()
MoveEntity point_light,0,3,0
SetLightRange point_light,10
SetLightShadowMappingEnabled point_light,True
SetEntityParent point_light,camera

SetAmbientLightColor 1,1,1,0.1

; create environment and skybox
env = LoadTexture("sgd://envmaps/sunnysky-cube.png", 4, 56)
; SetSceneEnvTexture env
skybox = CreateSkybox(env)
SetSkyboxRoughness skybox,.3

; create concrete
grass_material = LoadPBRMaterial("../engine/assets/materials/Grass001_1K-JPG")
grass_mesh = CreateBoxMesh(-64, -4.26, -64, 64, 0, 64,grass_material)
TFormMeshTexCoords grass_mesh, 8,8,0,0
grass = CreateModel(grass_mesh)
MoveEntity grass,0,0,128

; create grass
concrete_material = LoadPBRMaterial("../engine/assets/materials/Concrete031_1K-JPG")
concrete_mesh = CreateBoxMesh(-64, -4.26, -64, 64, -3.26, 64,concrete_material)
TFormMeshTexCoords concrete_mesh, 8,8,0,0
ground = CreateModel(concrete_mesh)

; add models

; main building 
building_model = LoadModel("../engine/assets/models/disc_golf/building.glb")

; fans
exercise_fan = LoadModel("../engine/assets/models/disc_golf/ceiling_fan.glb")
pool_fan = LoadModel("../engine/assets/models/disc_golf/ceiling_fan_pool.glb")

MoveEntity pool_fan,27,11,16
MoveEntity exercise_fan,10,2.5,7

; foofa = LoadModel("../engine/assets/models/disc_golf/foofa.glb")
; TurnEntity foofa,0,90,0
; MoveEntity foofa,0,0,5

; pool_building 
pool_building = LoadModel("../engine/assets/models/disc_golf/pool_building.glb")
MoveEntity pool_building,27.1,0,18

; pool speaker
pool_speaker = LoadModel("../engine/assets/models/disc_golf/speaker.glb")
MoveEntity pool_speaker,27,0.5,30

; outdoor pools
outdoor_pool_mesh = LoadMesh("../engine/assets/models/disc_golf/outdoor_pool.glb")
outdoor_pool = CreateModel(outdoor_pool_mesh)

MoveEntity outdoor_pool,45,0,40
TurnEntity outdoor_pool,0,180,0

outdoor_pool2 = CreateModel(outdoor_pool_mesh)
MoveEntity outdoor_pool2,0,0,40
TurnEntity outdoor_pool2,0,180,0

; BOUNCE ROOMS 
bounce_room_mesh = LoadMesh("../engine/assets/models/disc_golf/bounce_room.glb")
For x = 1 To 8
	m = CreateModel(bounce_room_mesh)
	MoveEntity m,x * 15 - 64,0,80
Next

; slides
slide_mesh = LoadMesh("../engine/assets/models/disc_golf/slide.glb")
For x = 1 To 5
	m = CreateModel(slide_mesh)
	m2 = CreateModel(slide_mesh)
	MoveEntity m,x * 20 - 64,0,100
	MoveEntity m2,x * 20 - 64,0,140
Next

; waterfalls 
waterfall = LoadModel("../engine/assets/models/disc_golf/waterfall.glb")
MoveEntity waterfall,x * 5,0,-5

; ferris wheel 
ferris_wheel.FerrisWheel = CreateFerrisWheel()
MoveEntity ferris_wheel\base_pivot,30,0,160

; race track
; grass for racetrack 

race_track_ground = CreateModel(grass_mesh)
MoveEntity race_track_ground,128,0,0
race_track = LoadModel("../engine/assets/models/disc_golf/race_track.glb")
MoveEntity race_track,128,0,0

; swings 
swings_mesh = LoadMesh("../engine/assets/models/disc_golf/swings.glb")
SetMeshShadowCastingEnabled swings_mesh,True
swings1 = CreateModel(swings_mesh)
MoveEntity swings1,-40,0,160
swings2 = CreateModel(swings_mesh)
MoveEntity swings2,-25,0,160
swings3 = CreateModel(swings_mesh)
MoveEntity swings3,-40,0,180
swings4 = CreateModel(swings_mesh)
MoveEntity swings4,-25,0,180

; desk
desk_mesh = LoadMesh("../engine/assets/models/disc_golf/desk.glb")
SetMeshShadowCastingEnabled desk_mesh,True
desk = CreateModel(desk_mesh)

; bubble maker 
bm_mesh = LoadMesh("../engine/assets/models/disc_golf/bubble_maker.glb")
SetMeshShadowCastingEnabled bm_mesh,True
bubble_maker = CreateModel(bm_mesh)

; snack machine 
snack_mesh = LoadMesh("../engine/assets/models/disc_golf/snack_machine.glb")
SetMeshShadowCastingEnabled snack_mesh,True
snack_machine = CreateModel(snack_mesh)
MoveEntity snack_machine,19,0,-4

; beverage machine 
bev_mesh = LoadMesh("../engine/assets/models/disc_golf/bev_machine.glb")
SetMeshShadowCastingEnabled bev_mesh,True
bev_machine = CreateModel(bev_mesh)
MoveEntity bev_machine,20.5,0,-4

; bubbles
Type Bubble
	Field sprite
	Field x#,y#,z#
	Field life 
End Type 

blue_bubble_image = LoadImage("../engine/assets/textures/misc/blue_bubble.png",1)
pink_bubble_image = LoadImage("../engine/assets/textures/misc/pink_bubble.png",1)

SetImageBlendMode blue_bubble_image,3
SetImageBlendMode pink_bubble_image,3

SetImageSpriteViewMode blue_bubble_image,1	
SetImageSpriteViewMode pink_bubble_image,1
	
Function CreateBubble.Bubble(image,e)
	Local b.Bubble = New Bubble
	b\sprite = CreateSprite(image)
	Local sc# = Rnd(0.01,0.1)
	ScaleEntity b\sprite,sc,sc,sc
	SetEntityPosition  b\sprite,GetEntityX(e),GetEntityY(e),GetEntityZ(e)
	b\x = Rnd(-0.02,0.02)
	b\y = Rnd(0.01,0.08)
	b\z = Rnd(-0.02,0.02)
	b\life = Rand(1000,7000)
	Return b
End Function 

Function UpdateBubbles()
	For b.Bubble = Each Bubble
		MoveEntity b\sprite,b\x,b\y,b\z
		b\life = b\life - 1
		If b\life = 0 Then 
			DestroyEntity b\sprite
			Delete b
		End If 	
	Next 		
End Function 

SetEntityParent bubble_maker,desk
MoveEntity bubble_maker,0,0.75,0
MoveEntity desk,-5,0,25
; TurnEntity desk,0,90,0

Global cam_turn_speed# = 0.1
Global cam_move_speed_keyboard# = 0.1
Global cam_move_speed_mouse# = 0.02
Global sprint# = 0.0

Function StandardMouseInput(e)
	; Mouse Input					
	If IsMouseButtonDown(0) ; left mouse button down
		SetMouseCursorMode 3 ; hide and lock the cursor
		If IsMouseButtonDown(1) ; right mouse button down (both are held down now)
			MoveEntity e, GetMouseVX() * cam_move_speed_mouse, -GetMouseVY() * cam_move_speed_mouse, 0	
		Else ; only left mouse button is down
			MoveEntity e, 0, 0, -GetMouseVY() * cam_move_speed_mouse
			TurnEntity e, 0, -GetMouseVX() * cam_turn_speed, 0			
		EndIf		
	ElseIf IsMouseButtonDown(1) ; only right mouse button is down
		SetMouseCursorMode 3
		TurnEntity e, -GetMouseVY() * cam_turn_speed, -GetMouseVX() * cam_turn_speed, 0	
	Else 
		SetMouseCursorMode 1 ; set the mouse cursor to normal
	End If	
End Function

Function CorrectEntityRoll(e)
	; prevent entity from rolling
	If (GetEntityRZ(e) < 0 Or GetEntityRZ(e) > 0) Then SetEntityRotation e, GetEntityRX(e), GetEntityRY(e), 0			
End Function

Function KeyBoardInputFirstPerson(e)	
	If (IsKeyDown(340) Or IsKeyDown(344)) Then sprint=0.3 Else sprint=0.0 ; SHIFT to sprint
	If (IsKeyDown(87) Or IsKeyDown(265)) Then MoveEntity e, 0, 0, cam_move_speed_keyboard + sprint ; w or up
	If (IsKeyDown(83) Or IsKeyDown(264)) Then MoveEntity e, 0, 0, -(cam_move_speed_keyboard + sprint) ; s or down 
   	If (IsKeyDown(65) Or IsKeyDown(263)) Then MoveEntity e, -(cam_move_speed_keyboard + sprint), 0, 0 ; a or left
	If (IsKeyDown(68) Or IsKeyDown(262)) Then MoveEntity e, cam_move_speed_keyboard+sprint, 0, 0 ; d or right
	
	; add support for moving cam up / down with the keys Q / E or Pageup / Pagedown	
	If (IsKeyDown(81) Or IsKeyDown(266)) Then MoveEntity e, 0, cam_move_speed_keyboard + sprint, 0 ; Q or Pageup
	If (IsKeyDown(69) Or IsKeyDown(267)) Then MoveEntity e, 0, -(cam_move_speed_keyboard+sprint), 0 ; E or Pagedown
End Function 

Function MoveEntityKeyboard(e)
	If (IsKeyDown(73)) Then MoveEntity e, 0, 0, cam_move_speed_keyboard + sprint ; w or up
	If (IsKeyDown(75)) Then MoveEntity e, 0, 0, -(cam_move_speed_keyboard + sprint) ; s or down 
   	If (IsKeyDown(74)) Then MoveEntity e, -(cam_move_speed_keyboard + sprint), 0, 0 ; a or left
	If (IsKeyDown(76)) Then MoveEntity e, cam_move_speed_keyboard+sprint, 0, 0 ; d or right
End Function 


t#=0.0
loop=True

bubble_maker_on = False
ceiling_fans_on = False 

switch = LoadSound("../engine/assets/audio/misc/switch.wav")
bubble_sound = LoadSound("../engine/assets/audio/misc/bubble_motor.mp3")
snack_beep = LoadSound("../engine/assets/audio/misc/snack_beep.mp3")
snack_chime = LoadSound("../engine/assets/audio/misc/snack_chime.mp3")
drink_beep = LoadSound("../engine/assets/audio/misc/drink_beep.mp3")
drink_chime = LoadSound("../engine/assets/audio/misc/drink_chime.mp3")

While(loop) 
	
	e = PollEvents()
	If e=1 Then loop=False ; window close clicked
	If IsKeyHit(256) Then loop = False
	
	If bubble_maker_on Then 
		If Rnd(1) > 0.5 Then 
			CreateBubble blue_bubble_image,bubble_maker	
		Else 
			CreateBubble pink_bubble_image,bubble_maker	
		End If 
	End If 	
	UpdateBubbles()
	UpdateFerrisWheel ferris_wheel
	
	If IsKeyHit(66) Then 
		If bubble_maker_on = True Then 
			bubble_maker_on = False 
			PlaySound switch
		Else 
			bubble_maker_on = True				
			PlaySound bubble_sound
		End If 
	End If 		
	
	If IsKeyHit(320) Then 
		StopAudio current_song
		current_song = LoadSound("justin_time_song.mp3")		
		PlaySound current_song 
	End If	
	
	If IsKeyHit(321) Then 
		StopAudio current_song
		current_song = LoadSound("poolie_ball_song.mp3")		
		PlaySound current_song 
	End If	
	
	If IsKeyHit(322) Then 
		StopAudio current_song
		current_song = LoadSound("i_like_to_dance.mp3")		
		PlaySound current_song 
	End If	
	
	If IsKeyHit(323) Then PlaySound snack_beep
	If IsKeyHit(324) Then PlaySound snack_chime
	If IsKeyHit(325) Then PlaySound drink_beep
	If IsKeyHit(326) Then PlaySound drink_chime


	; turn fans 	
	If ceiling_fans_on Then 	
		TurnEntity exercise_fan,0,10,0
		TurnEntity pool_fan,0,-10,0	
	End If 
	
	If IsKeyHit(67) Then 
		If ceiling_fans_on Then 
			ceiling_fans_on = False 
			PlaySound switch
		Else 
			ceiling_fans_on = True
			PlaySound switch
		End If 
	End If 		
	
	StandardMouseInput(camera)
	KeyBoardInputFirstPerson(camera)
	CorrectEntityRoll(camera)
	
	; MoveEntityKeyboard snack_machine
	
	RenderScene()	
	Clear2D()
	; Draw2DText "Pool Building " + GetEntityX(snack_machine) + " " + GetEntityY(snack_machine) + " " + GetEntityZ(snack_machine),10,10
		
	Present()
Wend
End